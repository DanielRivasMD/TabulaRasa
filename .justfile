####################################################################################################

_default:
  @just --choose

####################################################################################################

@abort:

####################################################################################################

@list:
  just --list

####################################################################################################

# print justfile
[group('just')]
@show:
  bat .justfile --language make

####################################################################################################

# edit justfile
[group('just')]
@edit:
  micro .justfile

####################################################################################################

# compile roadmap action items
[group('dev')]
@todor:
  if [[ ! -f 'ROADMAP.txt' ]]; then touch 'ROADMAP.txt'; fi
  sed -i '' '/^==================================================$/,/^==================================================$/d' ROADMAP.txt
  { printf '==================================================\n'; todor -s; printf '==================================================\n'; } >> ROADMAP.txt

####################################################################################################
# config
####################################################################################################

goapp := 'TabulaRasa'
goexe := 'tabularasa'
dir   := '.tabularasa'

####################################################################################################
# jobs
####################################################################################################

# build exec
[group('go')]
build app=goapp:
  @echo "\n\033[1;33mBuilding\033[0;37m...\n=================================================="
  go build -v -o excalibur/{{app}}

####################################################################################################

# install locally
[group('go')]
install app=goapp exe=goexe dir=dir:
  @echo "\n\033[1;33mInstalling\033[0;37m...\n=================================================="
  go install
  mv -v "${HOME}/go/bin/{{app}}" "${HOME}/go/bin/{{exe}}"
  @echo "\n\033[1;33mCopying\033[0;37m...\n=================================================="
  @if [ ! -d "${HOME}/{{dir}}" ]; then mkdir "${HOME}/{{dir}}"; fi
  @cp -v -R "skeleton/cobra" "${HOME}/{{dir}}"
  @cp -v -R "skeleton/config" "${HOME}/{{dir}}"
  @cp -v -R "skeleton/julia" "${HOME}/{{dir}}"
  @cp -v -R "skeleton/just" "${HOME}/{{dir}}"
  @cp -v -R "skeleton/readme" "${HOME}/{{dir}}"
  @cp -v -R "skeleton/todor" "${HOME}/{{dir}}"

####################################################################################################

# watch
[group('go')]
watch:
  watchexec --clear --watch cmd -- 'just install'

####################################################################################################

# print help
[group('go')]
@help:
  @echo "\n\033[1;33mPrinting\033[0;37m...\n=================================================="
  go run -- --help

####################################################################################################
